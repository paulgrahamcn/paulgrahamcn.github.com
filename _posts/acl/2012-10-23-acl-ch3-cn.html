---
layout: post
title : "第3章--列表"
category : acl
tags : [acl]
---
{% include JB/setup %}

列表是 Lisp 的基本数据结构之一。在最早的 Lisp 方言里，他们是唯一的数据结构： “Lisp” 这个名字起初是 “LISt Processor” 的缩写。但 Lisp 已经超越这个缩写很久了。 Common Lisp 是一个有着各式各样数据结构的通用性程序语言(general-purpose programming language)。<br/><br/>
Lisp 程序开发通常呼应着开发 Lisp 语言自身。在最初版本的 Lisp 程序，你可能使用很多列表。然而之后的版本，你可能换到快速、特定的数据结构。本章描述了你可以用列表所做的很多事情，以​​及使用它们来演示一些普遍的 Lisp 概念。<br/><br/>

<h1>3.1 构建 (Conses)</h1>
在 2.4 节我们介绍了 <tt class="docutils literal">cons</tt> , <tt class="docutils literal">car</tt> , 以及 <tt class="docutils literal">cdr</tt> ，基本的 List 操作函数 (list-manipulation fuctions)。 <tt class="docutils literal">cons</tt> 真正所做的事情是，把两个对象结合成一个有两部分的对象，称之为 <em>Cons</em> 对象。概念上来说，一个 <em>Cons</em> 是一对指针; 第一个是 <tt class="docutils literal">car</tt> ，第二个是 <tt class="docutils literal">cdr</tt> 。<br/><br/>
<em>Cons</em> 对象提供了一个方便的表示法来表示任何类型的对象。一个 <em>Cons</em> 对象里的一对指针可以指向任何类型的对象，包括 <em>Cons</em> 对象本身。它利用到我们之后可以用 <tt class="docutils literal">cons</tt> 来构建列表的可能性。<br/><br/>
我们往往不会把列表想成是成对的，但它们可以这样被定义。任何非空的列表，都可以被视为一对由列表第一个元素及列表其余元素所组成的列表。 Lisp 列表体现了这个概念。我们使用 <em>Cons</em> 的一半来指向列表的第一个元素，然后用另一半指向列表其余的元素(可能是别的 <em>Cons</em> 或nil)。 Lisp 的惯例是使用 <tt class="docutils literal">car</tt> 代表列表的第一个元素，而用 <tt class="docutils literal">cdr</tt> 代表列表的其余的元素。所以现在 <tt class="docutils literal">car</tt> 是列表的第一个元素的同义词，而 <tt class="docutils literal">cdr</tt> 是列表的其余的元素的同义词。列表不是不同的对象，而是像 <em>Cons</em> 这样的方式连结起来。<br/><br/>
当我们想在 <tt class="docutils literal">NIL</tt> 上面建立东西时，<br/><br/>

&gt; (setf x (cons 'a nil))
(A)
</pre>

<img alt="https://github.com/JuanitoFatas/acl-chinese/raw/master/images/Figure-3.1.png" src="https://github.com/JuanitoFatas/acl-chinese/raw/master/images/Figure-3.1.png" />

图 3.1 一个元素的列表<br/><br/>
产生的列表由一个 <em>Cons</em> 所组成，见图 3.1。这种表达 <em>Cons</em> 的方式叫做箱子表示法 (box notation)，因为每一个 Cons 是用一个箱子表示，内含一个 <tt class="docutils literal">car</tt> 和 <tt class="docutils literal">cdr</tt> 的指针。当我们调用 <tt class="docutils literal">car</tt> 与 <tt class="docutils literal">cdr</tt> 时，我们得到指针指向的地方：<br/><br/>

&gt; (car x)
A
&gt; (cdr x)
NIL
</pre>
当我们构建一个多元素的列表时，我们得到一串 <em>Cons</em> (a chain of conses):<br/><br/>

&gt; (setf y (list 'a 'b 'c))
(A B C)
</pre>
产生的结构见图 3.2。现在当我们想得到这个列表的 <tt class="docutils literal">cdr</tt> 时，它是一个两个元素的列表。<br/><br/>

<img alt="https://github.com/JuanitoFatas/acl-chinese/raw/master/images/Figure-3.2.png" src="https://github.com/JuanitoFatas/acl-chinese/raw/master/images/Figure-3.2.png" />

图 3.2 三个元素的列表<br/><br/>

&gt; (cdr y)
(B C)
</pre>
在一个有多个元素的列表中， <tt class="docutils literal">car</tt> 指针让你取得元素，而 <tt class="docutils literal">cdr</tt> 让你取得列表内其余的东西。<br/><br/>
一个列表可以有任何类型的对象作为元素，包括另一个列表：<br/><br/>

&gt; (setf z (list 'a (list 'b 'c) 'd))
(A (B C) D)
</pre>
当这种情况发生时，它的结构如图 3.3 所示；第二个 <em>Cons</em> 的 <tt class="docutils literal">car</tt> 指针也指向一个列表：<br/><br/>

&gt; (car (cdr z))
(B C)
</pre>

<img alt="https://github.com/JuanitoFatas/acl-chinese/raw/master/images/Figure-3.3.png" src="https://github.com/JuanitoFatas/acl-chinese/raw/master/images/Figure-3.3.png" />

图 3.3 嵌套列表<br/><br/>
前两个我们构建的列表都有三个元素; 只不过 <tt class="docutils literal">z</tt> 列表的第二个元素也刚好是一个列表。像这样的列表称为 <em>嵌套</em> 列表，而像 <tt class="docutils literal">y</tt> 这样的列表称之为 <em>平坦</em> 列表 ( <em>flat</em> list)。<br/><br/>
如果参数是一个 <em>Cons</em> 对象，函数 <tt class="docutils literal">consp</tt> 返回真。所以我们可以这样定义 <tt class="docutils literal">listp</tt> :<br/><br/>

(defun our-listp (x)
  (or (null x) (consp x)))
</pre>
因为所有不是 <em>Cons</em> 对象的东西就是一个原子 (atom)，判断式 <tt class="docutils literal">atom</tt> 可以这样定义：<br/><br/>

(defun our-atom (x) (not (consp x)))
</pre>
注意， <tt class="docutils literal">NIL</tt> 是一个原子，同时也是一个列表。<br/><br/>


<h1>3.2 等式 (Equality)</h1>
每一次你调用 <tt class="docutils literal">cons</tt> 时， Lisp 会配置一块新的内存给两个指针。所以如果我们用同样的参数调用 <tt class="docutils literal">cons</tt> 两次，我们得到两个数值看起来一样，但实际上是两个不同的对象：<br/><br/>

&gt; (eql (cons 'a nil) (cons 'a nil))
NIL
</pre>
如果我们也可以询问两个列表是否有相同元素，那就很方便了。 Common Lisp 提供了这种目的另一个判断式： <tt class="docutils literal">equal</tt> 。而另一方面 <tt class="docutils literal">eql</tt> 只有在它的参数是相同对象时才返回真，<br/><br/>

&gt; (setf x (cons 'a nil))
(A)
&gt; (eql x x)
T
</pre>
本质上 <tt class="docutils literal">equal</tt> 若它的参数打印出的值相同时，返回真：<br/><br/>

&gt; (equal x (cons 'a nil))
T
</pre>
这个判断式对非列表结构​​的别种对象也有效，但一种仅对列表有效的版本可以这样定义：<br/><br/>

&gt; (defun our-equal (x y)
    (or (eql x y)
        (and (consp x)
             (consp y)
             (our-equal (car x) (car y))
             (our-equal (cdr x) (cdr y)))))
</pre>
这个定义意味着，如果某个 <tt class="docutils literal">x</tt> 和 <tt class="docutils literal">y</tt> 相等( <tt class="docutils literal">eql</tt> )，那么他们也相等( <tt class="docutils literal">equal</tt> )。<br/><br/>
<strong>勘误:</strong> 这个版本的 <tt class="docutils literal"><span class="pre">our-equal</span></tt> 可以用在符号的列表 (list of symbols)，而不是列表 (list)。<br/><br/>


<h1>3.3 为什么 Lisp 没有指针 (Why Lisp Has No Pointers)</h1>
一个理解 Lisp 的秘密之一是意识到变量是有值的，就像列表有元素一样。如同 <em>Cons</em> 对象有指针指向他们的元素，变量有指针指向他们的值。<br/><br/>
你可能在别的语言中使用过显式指针 (explicitly pointer)。在 Lisp，你永远不用这么做，因为语言帮你处理好指针了。我们已经在列表看过这是怎么实现的。同样的事情发生在变量身上。举例来说，假设我们想要把两个变量设成同样的列表：<br/><br/>

 &gt; (setf x '(a b c))
(A B C)
&gt; (setf y x)
(A B C)
</pre>

<img alt="https://github.com/JuanitoFatas/acl-chinese/raw/master/images/Figure-3.4.png" src="https://github.com/JuanitoFatas/acl-chinese/raw/master/images/Figure-3.4.png" />

图 3.4 两个变量设为相同的列表<br/><br/>
当我们把 <tt class="docutils literal">x</tt> 的值赋给 <tt class="docutils literal">y</tt> 时，究竟发生什么事呢？内存中与 <tt class="docutils literal">x</tt> 有关的位置并没有包含这个列表，而是一个指针指向它。当我们给 <tt class="docutils literal">y</tt> 赋一个相同的值时， Lisp 复制的是指针，而不是列表。（图 3.4 显式赋值 <tt class="docutils literal">x</tt> 给 <tt class="docutils literal">y</tt> 后的结果）所以无论何时你把一个变量赋给另一个变量时，两个变量会有相同的值 ( <tt class="docutils literal">eql</tt> )。<br/><br/>

&gt; (eql x y)
T
</pre>
Lisp 没有指针的原因是因为每一个值，其实概念上来说都是一个指针。当你赋一个值给变量或将这个值存在数据结构中，其实被储存的是指向这个值的指针。当你要取得变量的值，或是存在数据结构中的内容时， Lisp 返回指向这个值的指针。但这都在台面下发生。你可以不加思索地把值放在结构里，或放 “在” 变量里。<br/><br/>
为了效率的原因， Lisp  有时会选择一个折衷的表示法，而不是指针。举例来说，因为一个小整数所需的内存空间，少于一个指针所需的空间，一个 Lisp 实现可能会直接处理这个小整数，而不是用指针来处理。但基本要点是，程序员预设可以把任何东西放在任何地方。除非你声明你不愿这么做，不然你能够在任何的数据结构，存放任何类型的对象，包括结构本身。<br/><br/>


<h1>3.4 建立列表 (Building Lists)</h1>

<img alt="https://github.com/JuanitoFatas/acl-chinese/raw/master/images/Figure-3.5.png" src="https://github.com/JuanitoFatas/acl-chinese/raw/master/images/Figure-3.5.png" />

图 3.5 复制的结果<br/><br/>
函数 <tt class="docutils literal"><span class="pre">copy-list</span></tt> 接受一个列表，然后返回此列表的复本。新的列表会有同样的元素，但是装在新的 <em>Cons</em> 对象里：<br/><br/>

&gt; (setf x '(a b c)
        y (copy-list x))
(A B C)
</pre>
图 3.5 展示出结果的结构; 返回值像是有着相同乘客的新公车。我们可以把 <tt class="docutils literal"><span class="pre">copy-list</span></tt> 想成是这么定义的:<br/><br/>

(defun our-copy-list (lst)
 (if (atom lst)
     lst
     (cons (car lst) (our-copy-list (cdr lst)))))
</pre>
这个定义暗示着 <tt class="docutils literal">x</tt> 与 <tt class="docutils literal"><span class="pre">(copy-list</span> x)</tt> 会永远 <tt class="docutils literal">equal</tt> ，并永远不 <tt class="docutils literal">eql</tt> ，除非 <tt class="docutils literal">x</tt> 是 <tt class="docutils literal">NIL</tt> 。<br/><br/>
最后，函数 <tt class="docutils literal">append</tt> 返回任何数目的列表串接 (concatenation)：<br/><br/>

&gt; (append '(a b) '(c d) 'e)
(A B C D E)
</pre>
通过这么做，它复制所有的参数，除了最后一个<br/><br/>


<h1>3.5 示例：压缩 (Example: Compression)</h1>
作为一个例子，这节将演示如何实现简单形式的列表压缩。这个算法有一个令人映像深刻的名字， <em>游程编码</em> (run-length encoding)。<br/><br/>

(defun compress (x)
  (if (consp x)
      (compr (car x) 1 (cdr x))
      x))

(defun compr (elt n lst)
  (if (null lst)
      (list (n-elts elt n))
      (let ((next (car lst)))
        (if (eql next elt)
            (compr elt (+ n 1) (cdr lst))
            (cons (n-elts elt n)
                  (compr next 1 (cdr lst)))))))

(defun n-elts (elt n)
  (if (&gt; n 1)
      (list n elt)
      elt))
</pre>
图 3.6 游程编码 (Run-length encoding)：压缩<br/><br/>
在餐厅的情境下，这个算法的工作方式如下。一个女服务生走向有四个客人的桌子。“你们要什么？” 她问。“我要特餐，” 第一个客人说。
“我也是，” 第二个客人说。“听起来不错，” 第三个客人说。每个人看着第四个客人。 “我要一个 cilantro soufflé，” 他小声地说。 (译注：蛋奶酥上面洒香菜跟酱料）<br/><br/>
瞬息之间，女服务生就转身踩着高跟鞋走回柜台去了。 “三个特餐，” 她大声对厨师说，“还有一个香菜蛋奶酥。”<br/><br/>
图 3.6 展示了如何实现这个压缩列表演算法。函数 <tt class="docutils literal">compress</tt> 接受一个由原子组成的列表，然后返回一个压缩的列表：<br/><br/>

&gt; (compress '(1 1 1 0 1 0 0 0 0 1))
((3 1) 0 1 (4 0) 1)
</pre>
当相同的元素连续出现好几次，这个连续出现的序列 (sequence)被一个列表取代，列表指明出现的次数及出现的元素。<br/><br/>
主要的工作是由递归函数 <tt class="docutils literal">compr</tt> 所完成。这个函数接受三个参数： <tt class="docutils literal">elt</tt> ， 上一个我们看过的元素； <tt class="docutils literal">n</tt> ，连续出现的次数；以及 <tt class="docutils literal">lst</tt> ，我们还没检视过的部分列表。如果没有东西需要检视了，我们调用 <tt class="docutils literal"><span class="pre">n-elts</span></tt> 来取得 <tt class="docutils literal">n elts</tt> 的表示法。如果 <tt class="docutils literal">lst</tt> 的第一个元素还是 <tt class="docutils literal">elt</tt> ，我们增加出现的次数 <tt class="docutils literal">n</tt> 并继续下去。否则我们得到，到目前为止的一个压缩的列表，然后 <tt class="docutils literal">cons</tt> 这个列表在 <tt class="docutils literal">compr</tt> 处理完剩下的列表所返回的东西之上。<br/><br/>
要复原一个压缩的列表，我们调用 <tt class="docutils literal">uncompress</tt> (图 3.7)<br/><br/>

&gt; (uncompress '((3 1) 0 1 (4 0) 1))
(1 1 1 0 1 0 0 0 0 1)
</pre>

(defun uncompress (lst)
  (if (null lst)
      nil
      (let ((elt (car lst))
            (rest (uncompress (cdr lst))))
        (if (consp elt)
            (append (apply #'list-of elt)
                    rest)
            (cons elt rest)))))

(defun list-of (n elt)
  (if (zerop n)
      nil
      (cons elt (list-of (- n 1) elt))))
</pre>
图 3.7 游程编码 (Run-length encoding)：解压缩<br/><br/>
这个函数递归地遍历这个压缩列表，逐字复制原子并调用 <tt class="docutils literal"><span class="pre">list-of</span></tt> ，展开成列表。<br/><br/>

&gt; (list-of 3 'ho)
(HO HO HO)
</pre>
我们其实不需要自己写 <tt class="docutils literal"><span class="pre">list-of</span></tt> 。内置的 <tt class="docutils literal"><span class="pre">make-list</span></tt> 可以办到一样的事情─但它使用了我们还没介绍到的关键字参数 (keyword argument)。<br/><br/>
图 3.6 跟 3.7 这种写法不是一个有经验的Lisp 程序员用的写法。它的效率很差，它没有尽可能的压缩，而且它只对由原子组成的列表有效。在几个章节内，我们会学到解决这些问题的技巧。<br/><br/>

载入程序

在这节的程序是我们第一个实质的程序。
当我们想要写超过数行的函数时，
通常我们会把程序写在一个文件，
然后使用 load 让 Lisp 读取函数的定义。
如果我们把图 3.6 跟 3.7 的程序，
存在一个档案叫做，&quot;compress.lisp&quot; 然后输入

(load &quot;compress.lisp&quot;)

到顶层，或多或少的，
我们会像在直接输入顶层一样得到同样的效果。

注意：在某些实现中，Lisp 文件的扩展名会是&quot;.lsp&quot; 而不是&quot;.lisp&quot;。
</pre>


<h1>3.6 存取 (Access)</h1>
Common Lisp 有额外的存取函数，它们是用 <tt class="docutils literal">car</tt> 跟 <tt class="docutils literal">cdr</tt> 所定义的。要找到列表特定位置的元素，我们可以调用 <tt class="docutils literal">nth</tt> ，<br/><br/>

&gt; (nth 0 '(a b c))
A
</pre>
而要找到第 <tt class="docutils literal">n</tt> 个 <tt class="docutils literal">cdr</tt> ，我们调用 <tt class="docutils literal">nthcdr</tt> ：<br/><br/>

&gt; (nthcdr 2 '(a b c))
(C)
</pre>
<tt class="docutils literal">nth</tt> 与 <tt class="docutils literal">nthcdr</tt> 都是零索引的 (zero-indexed); 即元素从 <tt class="docutils literal">0</tt> 开始编号，而不是从 <tt class="docutils literal">1</tt> 开始。在 Common Lisp 里，无论何时你使用一个数字来参照一个数据结构中的元素时，都是从 <tt class="docutils literal">0</tt> 开始编号的。<br/><br/>
两个函数几乎做一样的事; <tt class="docutils literal">nth</tt> 等同于取 <tt class="docutils literal">nthcdr</tt> 的 <tt class="docutils literal">car</tt> 。没有检查错误的情况下， <tt class="docutils literal">nthcdr</tt> 可以这么定义：<br/><br/>

(defun our-nthcdr (n lst)
  (if (zerop n)
      lst
      (our-nthcdr (- n 1) (cdr lst))))
</pre>
函数 <tt class="docutils literal">zerop</tt> 仅在参数为零时，才返回真。<br/><br/>
函数 <tt class="docutils literal">last</tt> 返回列表的最后一个 <em>Cons</em> 对象：<br/><br/>

&gt; (last '(a b c))
(C)
</pre>
这跟取得最后一个元素不一样。要取得列表的最后一个元素，你要取得 <tt class="docutils literal">last</tt> 的 <tt class="docutils literal">car</tt> 。<br/><br/>
Common Lisp 定义了函数 <tt class="docutils literal">first</tt> 直到 <tt class="docutils literal">tenth</tt> 可以取得列表对应的元素。这些函数不是 <em>零索引的</em> (zero-indexed)：<br/><br/>
<tt class="docutils literal">(second x)</tt> 等同于 <tt class="docutils literal">(nth 1 x)</tt> 。<br/><br/>
此外， Common Lisp 定义了像是 <tt class="docutils literal">caddr</tt> 这样的函数，它是 <tt class="docutils literal">cdr</tt> 的 <tt class="docutils literal">cdr</tt> 的 <tt class="docutils literal">car</tt> 的缩写 (car of cdr of cdr)。所有这样形式的函数 <tt class="docutils literal">cxr</tt> ，其中 x 是一个字串，最多四个 <tt class="docutils literal">a</tt> 或 <tt class="docutils literal">d</tt> ，在 Common Lisp 里都被定义好了。使用 <tt class="docutils literal">cadr</tt> 可能会有异常 (exception)产生，在所有人都可能会读的代码里使用这样的函数，不是一个好的主意。<br/><br/>


<h1>3.7 映射函数 (Mapping Functions)</h1>
Common Lisp 提供了数个函数来对一个列表的元素做函数调用。最常使用的是 <tt class="docutils literal">mapcar</tt> ，接受一个函数以及一个或多个列表，并返回把函数应用至每个列表的元素的结果，直到有的列表没有元素为止：<br/><br/>

&gt; (mapcar #'(lambda (x) (+ x 10))
          '(1 2 3))
(11 12 13)

&gt; (mapcar #'list
          '(a b c)
          '(1 2 3 4))
((A 1) (B 2) (C 3))
</pre>
相关的 <tt class="docutils literal">maplist</tt> 接受同样的参数，将列表的渐进的下一个 <tt class="docutils literal">cdr</tt> 传入函数。<br/><br/>

&gt; (maplist #'(lambda (x) x)
           '(a b c))
((A B C) (B C) (C))
</pre>
其它的映成函数，包括 <tt class="docutils literal">mapc</tt> 我们在 89 页讨论（译注：5.4 节最后），以及 <tt class="docutils literal">mapcan</tt> 在 202 页（译注：12.4 节最后）讨论。<br/><br/>


<h1>3.8 树 (Trees)</h1>
<em>Cons</em> 对象可以想成是二元树， <tt class="docutils literal">car</tt> 代表右子树，而 <tt class="docutils literal">cdr</tt> 代表左子树。举例来说，列表<br/><br/>
<tt class="docutils literal">(a (b c) d)</tt> 也是一棵由图 3.8 所代表的树。 （如果你逆时针旋转 45 度，你会发现跟图 3.3 一模一样）<br/><br/>

<img alt="https://github.com/JuanitoFatas/acl-chinese/raw/master/images/Figure-3.8.png" src="https://github.com/JuanitoFatas/acl-chinese/raw/master/images/Figure-3.8.png" />

图 3.8 二元树 (Binary Tree)<br/><br/>
Common Lisp 有几个内置的操作树的函数。举例来说， <tt class="docutils literal"><span class="pre">copy-tree</span></tt> 接受一个树，并返回一份副本。它可以这么定义：<br/><br/>

(defun our-copy-tree (tr)
  (if (atom tr)
       tr
       (cons (our-copy-tree (car tr))
             (our-copy-tree (cdr tr)))))
</pre>
把这跟 36 页的 <tt class="docutils literal"><span class="pre">copy-list</span></tt> 比较一下； <tt class="docutils literal"><span class="pre">copy-tree</span></tt> 复制每一个 <em>Cons</em> 对象的 <tt class="docutils literal">car</tt> 与 <tt class="docutils literal">cdr</tt> ，而 <tt class="docutils literal"><span class="pre">copy-list</span></tt> 仅复制 <tt class="docutils literal">cdr</tt> 。<br/><br/>
没有内部节点的二元树没有太大的用处。 Common Lisp 包含了操作树的函数，不只是因为我们需要树这个结构，而是因为我们需要一种方法，来操作列表及所有内部的列表。举例来说，假设我们有一个这样的列表：<br/><br/>

(and (integerp x) (zerop (mod x 2)))
</pre>
而我们想要把各处的 <tt class="docutils literal">x</tt> 都换成 <tt class="docutils literal">y</tt> 。调用 <tt class="docutils literal">substitute</tt> 是不行的，它只能替换序列 (sequence)中的元素：<br/><br/>

&gt; (substitute 'y 'x '(and (integerp x) (zerop (mod x 2))))
(AND (INTEGERP X) (ZEROP (MOD X 2)))
</pre>
这个调用是无效的，因为列表有三个元素，没有一个元素是 <tt class="docutils literal">x</tt> 。我们在这所需要的是 <tt class="docutils literal">subst</tt> ，它替换树之中的元素。<br/><br/>

&gt; (subst 'y 'x '(and (integerp x) (zerop (mod x 2))))
(AND (INTEGERP Y) (ZEROP (MOD Y 2)))
</pre>
如果我们定义一个 <tt class="docutils literal">subst</tt> 的版本，它看起来跟 <tt class="docutils literal"><span class="pre">copy-tree</span></tt> 很相似：<br/><br/>

&gt; (defun our-subst (new old tree)
    (if (eql tree old)
        new
        (if (atom tree)
            tree
            (cons (our-subst new old (car tree))
                  (our-subst new old (cdr tree))))))
</pre>
操作树的函数通常有这种形式， <tt class="docutils literal">car</tt> 与 <tt class="docutils literal">cdr</tt> 同时做递归。这种函数被称之为是 <em>双重递归</em> (doubly recursive)。<br/><br/>


<h1>3.9 理解递归 (Understanding Recursion)</h1>
学生在学习递归时，有时候是被鼓励在纸上追踪 (trace)递归程序调用 (invocation)的过程。 (288页「译注：Appendix A Trace and Backtraces」可以看到一个递归函数的追踪过程。)但这种练习可能会误导你：一个程序员在定义一个递归函数时，通常不会特别地去想函数的调用顺序所导致的结果。<br/><br/>
如果一个人总是需要这样子思考程序，递归会是艰难的、没有帮助的。递归的优点是它精确地让我们更抽象地来检视算法。你不需要考虑真正函数时所有的调用 (invocation)过程，就可以判断一个递归函数是否​​是正确的。<br/><br/>
要知道一个递归函数是否​​做它该做的事，你只需要问，它包含了所有的情况吗？举例来说，下面是一个寻找列表长度的递归函数：<br/><br/>

&gt; (defun len (lst)
    (if (null lst)
        0
        (+ (len (cdr lst)) 1)))
</pre>
我们可以藉由检查两件事情，来确信这个函数是正确的：<br/><br/>
<ol class="arabic simple">
<li>对长度为 <tt class="docutils literal">0</tt> 的列表是有效的。</li>
<li>给定它对于长度为 <tt class="docutils literal">n</tt> 的列表是有效的，它对长度是 <tt class="docutils literal">n+1</tt> 的列表也是有效的。</li>
</ol>
如果这两点是成立的，我们知道这个函数对于所有可能的列表都是正确的。<br/><br/>
我们的定义显然地满足第一点：如果列表( <tt class="docutils literal">lst</tt> ) 是空的( <tt class="docutils literal">nil</tt> )，函数直接返回 <tt class="docutils literal">0</tt> 。现在假定我们的函数对长度为 <tt class="docutils literal">n</tt> 的列表是有效的。我们给它一个 <tt class="docutils literal">n+1</tt> 长度的列表。这个定义说明了，函数会返回列表的 <tt class="docutils literal">cdr</tt> 的长度再加上 <tt class="docutils literal">1</tt> 。 <tt class="docutils literal">cdr</tt> 是一个长度为 <tt class="docutils literal">n</tt> 的列表。我们经由假定可知它的长度是 <tt class="docutils literal">n</tt> 。所以整个列表的长度是 <tt class="docutils literal">n+1</tt> 。<br/><br/>
我们需要知道的就是这些。理解递归的秘密就像是处理括号一样。你怎么知道哪个括号对上哪个？你不需要这么做。你怎么想像那些调用过程？你不需要这么做。<br/><br/>
更复杂的递归函数，可能会有更多的情况需要讨论，但是流程是一样的。举例来说， 41 页的 <tt class="docutils literal"><span class="pre">our-copy-tree</span></tt> ，我们需要讨论三个情况： 原子，单一的 <em>Cons</em> 对象， <tt class="docutils literal">n+1</tt> 的 <em>Cons</em> 树。<br/><br/>
第一个情况（长度零的列表）称之为 <em>基本用例</em> ( <em>base case</em> )。当一个递归函数不像你想的那样工作时，通常是因为基本用例是错的。下面这个不正确的 <tt class="docutils literal">member</tt> 定义，是一个常见的​​错误，整个忽略了基本用例：<br/><br/>

(defun our-member (obj lst)
  (if (eql (car lst) obj)
      lst
      (our-member obj (cdr lst))))
</pre>
我们需要初始一个 <tt class="docutils literal">null</tt> 测试，确保在到达列表底部时，没有找到目标时要停止递归。如果我们要找的对象没有在列表里，这个版本的 <tt class="docutils literal">member</tt> 会陷入无穷回圈。附录 A 更详细地检视了这种问题。<br/><br/>
能够判断一个递归函数是否​​正确只不过是理解递归的上半场，下半场是能够写出一个做你想做的事情的递归函数。 6.9 节讨论了这个问题。<br/><br/>


<h1>3.10 集合 (Sets)</h1>
列表是表示小集合 (small sets)的好方法。列表中的每个元素都代表了一个集合的成员：<br/><br/>

&gt; (member 'b '(a b c))
(B C)
</pre>
当 <tt class="docutils literal">member</tt> 要返回“真”时，与其仅仅返回 <tt class="docutils literal">t</tt> ，它返回由寻找对象所开始的那部分。逻辑上来说，一个 <em>Cons</em> 扮演的角色和 <tt class="docutils literal">t</tt> 一样，而经由这么做，函数返回了更多资讯。<br/><br/>
一般情况下， <tt class="docutils literal">member</tt> 使用 <tt class="docutils literal">eql</tt> 来比较对象。你可以使用一种叫做关键字参数 (keyword argument)的东西来重写(override) 缺省的比较方法。多数的 Common Lisp 函数接受一个或多个关键字参数。这些关键字参数不同的地方是，他们不是把对应的参数放在特定的位置作匹配，而是在函数调用中用特殊标签，称为关键字，来作匹配。一个关键字是一个前面有冒号的符号。<br/><br/>
一个 <tt class="docutils literal">member</tt> 函数所接受的关键字参数是 <tt class="docutils literal">:test</tt> 参数。<br/><br/>
如果你在调用 <tt class="docutils literal">member</tt> 时，传入某个函数作为 <tt class="docutils literal">:test</tt> 参数，那么那个函数就会被用来比较是否相等，而不是用 <tt class="docutils literal">eql</tt> 。所以如果我们想找到一个给定的对象与列表中的成员是否相等( <tt class="docutils literal">equal</tt> )，我们可以：<br/><br/>

&gt; (member '(a) '((a) (z)) :test #'equal)
((A) (Z))
</pre>
关键字参数总是选​​择性添加的。如果你在一个调用中包含了任何的关键字参数，他们要摆在最后; 如果使用了超过一个的关键字参数，摆放的顺序无关紧要。<br/><br/>
另一个 <tt class="docutils literal">member</tt> 接受的关键字参数是 <tt class="docutils literal">:key</tt> 参数。藉由提供这个参数，你可以在作比较之前，指定一个函数运用在每一个元素：<br/><br/>

&gt; (member 'a '((a b) (c d)) :key #'car)
((A B) (C D))
</pre>
在这个例子里，我们询问是否有一个元素的 <tt class="docutils literal">car</tt> 是 <tt class="docutils literal">a</tt> ​​。<br/><br/>
如果我们想要使用两个关键字参数，我们可以使用其中一个顺序。下面这两个调用是等价的：<br/><br/>

&gt; (member 2 '((1) (2)) :key #'car :test #'equal)
((2))
&gt; (member 2 '((1) (2)) :test #'equal :key #'car)
((2))
</pre>
两者都询问是否有一个元素的 <tt class="docutils literal">car</tt> 等于( <tt class="docutils literal">equal</tt> ) 2。<br/><br/>
如果我们想要找到一个元素满足任意的判断式像是── <tt class="docutils literal">oddp</tt> ，奇数返回真──我们可以使用相关的 <tt class="docutils literal"><span class="pre">member-if</span></tt> ：<br/><br/>

&gt; (member-if #'oddp '(2 3 4))
(3 4)
</pre>
我们可以想像一个限制性的版本 <tt class="docutils literal"><span class="pre">member-if</span></tt> 是这样写成的：<br/><br/>

defun our-member-if (fn lst)
  (and (consp lst)
       (if (funcall fn (car lst))
           lst
           (our-member-if fn (cdr lst)))))
</pre>
函数 <tt class="docutils literal">adjoin</tt> 像是条件式的 <tt class="docutils literal">cons</tt> (conditional <tt class="docutils literal">cons</tt> )。它接受一个对象及一个列表，如果对象还不是列表的成员，才构建对象至列表上。<br/><br/>

&gt; (adjoin 'b '(a b c))
(A B C)
&gt; (adjoin 'z '(a b c))
(Z A B C)
</pre>
通常的情况下它接受与 <tt class="docutils literal">member</tt> 函数同样的关键字参数。<br/><br/>
集合论中的并集 (union)、交集 (intersection)以及补集 (complement)的实现是由函数 <tt class="docutils literal">union</tt> 、 <tt class="docutils literal">intersection</tt> 以及 <tt class="docutils literal"><span class="pre">set-difference</span></tt> 。<br/><br/>
这些函数期望两个 (正好 2 个)列表（一样接受与 <tt class="docutils literal">member</tt> 函数同样的关键字参数）。<br/><br/>

&gt; (union '(a b c) '(c b s))
(A C B S)
&gt; (intersection '(a b c) '(b b c))
(B C)
&gt; (set-difference '(a b c d e) '(b e))
(A C D)
</pre>
因为集合中没有顺序的概念，这些函数不需要保留原本元素在列表被找到的顺序。举例来说，调用 <tt class="docutils literal"><span class="pre">set-difference</span></tt> 也有可能返回 <tt class="docutils literal">(d c a)</tt> 。<br/><br/>


<h1>3.11 序列 (Sequences)</h1>
另一种考虑一个列表的方式是想成一系列有特定顺序的对象。在 Common Lisp 里， <em>序列</em> ( <em>sequences</em> )包括了列表与向量 (vectors)。本节介绍了一些可以运用在列表上的序列函数。更深入的序列操作在 4.4 节讨论。<br/><br/>
函数 <tt class="docutils literal">length</tt> 返回序列中元素的数目。<br/><br/>

&gt; (length '(a b c))
3
</pre>
我们在 24 页 (译注：2.13节 <tt class="docutils literal"><span class="pre">our-length</span></tt> )写过这种函数的一个版本（仅可用于列表）。<br/><br/>
要复制序列的一部分，我们使用 <tt class="docutils literal">subseq</tt> 。第二个（需要的）参数是第一个开始引用进来的元素位置，第三个（选择性）参数是第一个不引用进来的元素位置。<br/><br/>

&gt; (subseq '(a b c d) 1 2)
(B)
&gt;(subseq '(a b c d) 1)
(B C D)
</pre>
如果省略了第三个参数，子序列会从第二个参数给定的位置引用到序列尾端。<br/><br/>
函数 <tt class="docutils literal">reverse</tt> 返回与其参数相同元素的一个序列，但顺序颠倒。<br/><br/>

&gt; (reverse '(a b c))
(C B A)
</pre>
一个回文 (palindrome) 是一个正读反读都一样的序列 —— 举例来说， <tt class="docutils literal">(abba)</tt> 。如果一个回文有偶数个元素，那么后半段会是前半段的镜射 (mirror)。使用 <tt class="docutils literal">length</tt> 、 <tt class="docutils literal">subseg</tt> 以及 <tt class="docutils literal">reverse</tt> ，我们可以定义一个函数<br/><br/>

(defun mirror? (s)
  (let ((len (length s)))
    (and (evenp len)
         (let ((mid (/ len 2)))
           (equal (subseq s 0 mid)
                  (reverse (subseq s mid)))))))
</pre>
来检测是否是回文：<br/><br/>

&gt; (mirror? '(a b b a))
T
</pre>
Common Lisp 有一个内置的排序函数叫做 <tt class="docutils literal">sort</tt> 。它接受一个序列及一个比较两个参数的函数，返回一个有同样元素的序列，根据比较函数来排序：<br/><br/>

&gt; (sort '(0 2 1 3 8) #'&gt;)
(8 3 2 1 0)
</pre>
你要小心使用 <tt class="docutils literal">sort</tt> ，因为它是 <em>破坏性的</em> ( <em>destructive</em> )。考虑到效率的因素， <tt class="docutils literal">sort</tt> 被允许修改传入的序列。所以如果你不想你本来的序列被改动，传入一个副本。<br/><br/>
使用 <tt class="docutils literal">sort</tt> 及 <tt class="docutils literal">nth</tt> ，我们可以写一个函数，接受一个整数 <tt class="docutils literal">n</tt> ，返回列表中第 <tt class="docutils literal">n</tt> 大的元素：<br/><br/>

(defun nthmost (n lst)
  (nth (- n 1)
       (sort (copy-list lst) #'&gt;)))
</pre>
我们把整数减一因为 <tt class="docutils literal">nth</tt> 是零索引的，但如果 <tt class="docutils literal">nthmost</tt> 是这样的话，会变得很不直观。<br/><br/>

(nthmost 2 '(0 2 1 3 8))
</pre>
多努力一点，我们可以写出这个函数的一个更有效率的版本。<br/><br/>
函数 <tt class="docutils literal">every</tt> 和 <tt class="docutils literal">some</tt> 接受一个判断式及一个或多个序列。当我们仅输入一个序列时，它们测试序列元素是否满足判断式：<br/><br/>

&gt; (every #'oddp '(1 3 5))
T
&gt; (some #'evenp '(1 2 3))
T
</pre>
如果它们输入多于一个序列时，判断式必须接受与序列一样多的元素作为参数，而参数从所有序列中一次提取一个：<br/><br/>

&gt; (every #'&gt; '(1 3 5) '(0 2 4))
T
</pre>
如果序列有不同的长度，最短的那个序列，决定需要测试的次数。<br/><br/>


<h1>3.12 栈 (Stacks)</h1>
用 <em>Cons</em> 对象来表示的列表，很自然地我们可以拿来实现下推栈 (pushdown stack)。这太常见了，以致于 Common Lisp 提供了两个宏给堆叠使用： <tt class="docutils literal">(push xy)</tt> 把 x 放入列表 y 的前端。而 <tt class="docutils literal">(pop x)</tt> 则是将列表 x 的第一个元素移除，并返回这个元素。<br/><br/>
两个函数都是由 <tt class="docutils literal">setf</tt> 定义的。如果参数是常数或变量，很简单就可以翻译出对应的函数调用。<br/><br/>
表达式<br/><br/>
<tt class="docutils literal">(push obj lst)</tt><br/><br/>
等同于<br/><br/>
<tt class="docutils literal">(setf lst (cons obj lst))</tt><br/><br/>
而表达式<br/><br/>
<tt class="docutils literal">(pop lst)</tt><br/><br/>
等同于<br/><br/>

(let ((x (car lst)))
  (setf lst (cdr lst))
  x)
</pre>
所以，举例来说：<br/><br/>

&gt; (setf x '(b))
(B)
&gt; (push 'a x)
(A B)
&gt; x
(A B)
&gt; (setf y x)
(A B)
&gt; (pop x)
(A)
&gt; x
(B)
&gt; y
(A B)
</pre>
以上，全都遵循上述由 <tt class="docutils literal">setf</tt> 所给出的相等式。图 3.9 展示了这些表达式被求值后的结构。<br/><br/>

<img alt="https://github.com/JuanitoFatas/acl-chinese/raw/master/images/Figure-3.9.png" src="https://github.com/JuanitoFatas/acl-chinese/raw/master/images/Figure-3.9.png" />

图 3.9 push 及 pop 的效果<br/><br/>
你可以使用 <tt class="docutils literal">push</tt> 来定义一个给列表使用的互动版 <tt class="docutils literal">reverse</tt> 。<br/><br/>

(defun our-reverse (lst)
  (let ((acc nil))
    (dolist (elt lst)
      (push elt acc))
    acc))
</pre>
在这个版本，我们从一个空列表开始，然后把 <tt class="docutils literal">lst</tt> 的每一个元素放入空表里。等我们完成时，<tt class="docutils literal">lst</tt> 最后一个元素会在最前端。<br/><br/>
<tt class="docutils literal">pushnew</tt> 宏是 <tt class="docutils literal">push</tt> 的变种，使用了 <tt class="docutils literal">adjoin</tt> 而不是 <tt class="docutils literal">cons</tt> ：<br/><br/>

 &gt; (let ((x '(a b)))
     (pushnew 'c x)
     (pushnew 'a x)
     x)
(C A B)
</pre>
在这里， <tt class="docutils literal">c</tt> 被放入列表，但是 <tt class="docutils literal">a</tt> 没有，因为它已经是列表的一个成员了。<br/><br/>


<h1>3.13 点状列表 (Dotted Lists)</h1>
调用 <tt class="docutils literal">list</tt> 所构建的列表，这种列表精确地说称之为正规列表( <em>proper</em> list )。一个正规列表可以是 <tt class="docutils literal">NIL</tt> 或是 <tt class="docutils literal">cdr</tt> 是正规列表的 <em>Cons</em> 对象。也就是说，我们可以定义一个只对正规列表返回真的判断式： <a class="footnote-reference" href="#id4" id="id1">[3]</a><br/><br/>

(defun proper-list? (x)
  (or (null x)
      (and (consp x)
           (proper-list? (cdr x)))))
</pre>
至目前为止，我们构建的列表都是正规列表。<br/><br/>
然而， <tt class="docutils literal">cons</tt> 不仅是构建列表。无论何时你需要一个具有两个变量 (field)的列表，你可以使用一个 <em>Cons</em> 对象。你能够使用 <tt class="docutils literal">car</tt> 来参照第一个变量，用 <tt class="docutils literal">cdr</tt> 来参照第二个变量。<br/><br/>

&gt; (setf pair (cons 'a 'b))
(A . B)
</pre>
因为这个 <em>Cons</em> 对象不是一个正规列表，它用点状表示法来显示。在点状表示法，每个 <em>Cons</em> 对象的 <tt class="docutils literal">car</tt> 与 <tt class="docutils literal">cdr</tt> 由一个句点隔开来表示。这个 <em>Cons</em> 对象的结构展示在图 3.10 。<br/><br/>

<img alt="https://github.com/JuanitoFatas/acl-chinese/raw/master/images/Figure-3.10.png" src="https://github.com/JuanitoFatas/acl-chinese/raw/master/images/Figure-3.10.png" />

图3.10 一个成对的 <em>Cons</em> 对象 (A cons used as a pair)<br/><br/>
一个非正规列表的 <em>Cons</em> 对象称之为点状列表 (dotted list)。这不是个好名字，因为非正规列表的 Cons 对象通常不是用来表示列表： <tt class="docutils literal">(a . b)</tt> 只是一个有两部分的数据结构。<br/><br/>
你也可以用点状表示法表示正规列表，但当 Lisp 显示一个正规列表时，它会使用普通的列表表示法：<br/><br/>

&gt; '(a . (b . (c . nil)))
(A B C)
</pre>
顺道一提，注意列表由点状表示法与图 3.2 箱子表示法的关联性。<br/><br/>
还有一个过渡形式 (intermediate form)的表示法，介于列表表示法及纯点状表示法之间，对于 <tt class="docutils literal">cdr</tt> 是点状列表的 <em>Cons</em> 对象：<br/><br/>

&gt; (cons 'a (cons 'b (cons 'c 'd)))
(A B C . D)
</pre>

<img alt="https://github.com/JuanitoFatas/acl-chinese/raw/master/images/Figure-3.11.png" src="https://github.com/JuanitoFatas/acl-chinese/raw/master/images/Figure-3.11.png" />

图 3.11 一个点状列表 (A dotted list)<br/><br/>
这样的 <em>Cons</em> 对象看起来像正规列表，除了最后一个 cdr 前面有一个句点。这个列表的结构展示在图 3.11 ; 注意它跟图3.2 是多么的相似。<br/><br/>
所以实际上你可以这么表示列表 <tt class="docutils literal">(a b)</tt> ，<br/><br/>

(a . (b . nil))
(a . (b))
(a b . nil)
(a b)
</pre>
虽然 Lisp 总是使用后面的形式，来显示这个列表。<br/><br/>


<h1>3.14 关联列表 (Assoc-lists)</h1>
用 <em>Cons</em> 对象来表示映射 (mapping)也是很自然的。一个由 <em>Cons</em> 对象组成的列表称之为 <em>关联列表</em> ( <em>assoc-list</em> or <em>alist</em> )。这样的列表可以表示一个翻译的集合，举例来说：<br/><br/>

&gt; (setf trans '((+ . &quot;add&quot;) (- . &quot;subtract&quot;)))
((+ . &quot;add&quot;) (- . &quot;subtract&quot;))
</pre>
关联列表很慢，但是在初期的程序中很方便。 Common Lisp 有一个内置的函数 <tt class="docutils literal">assoc</tt> ，用来取出在关联列表中，与给定的键值有关联的 <em>Cons</em> 对：<br/><br/>

&gt; (assoc '+ trans)
(+ . &quot;add&quot;)
&gt; (assoc '* trans)
NIL
</pre>
如果 <tt class="docutils literal">assoc</tt> 没有找到要找的东西时，返回 <tt class="docutils literal">nil</tt> 。<br/><br/>
我们可以定义一个受限版本的 <tt class="docutils literal">assoc</tt> ：<br/><br/>

(defun our-assoc (key alist)
  (and (consp alist)
       (let ((pair (car alist)))
        (if (eql key (car pair))
            pair
            (our-assoc key (cdr alist))))))
</pre>
和 <tt class="docutils literal">member</tt> 一样，实际上的 <tt class="docutils literal">assoc</tt> 接受关键字参数，包括 <tt class="docutils literal">:test</tt> 和 <tt class="docutils literal">:key</tt> 。 Common Lisp 也定义了一个 <tt class="docutils literal"><span class="pre">assoc-if</span></tt> 之于 <tt class="docutils literal">assoc</tt> ，如同 <tt class="docutils literal"><span class="pre">member-if</span></tt> 之于 <tt class="docutils literal">member</tt> 一样。<br/><br/>


<h1>3.15 示例：最短路径 (Example: Shortest Path)</h1>
图 3.12 包含一个搜索网络中最短路径的程序。函数 <tt class="docutils literal"><span class="pre">shortest-path</span></tt> 接受一个起始节点，目的节点以及一个网络，并返回最短路径，如果有的话。<br/><br/>
在这个范例中，节点用符号表示，而网络用含以下元素形式的关联列表 (assoc-lists)来表示：<br/><br/>
<em>(node​​ . neighbors)</em><br/><br/>
所以由图 3.13 展示的最小网络 (minimal network)可以这样来表示：<br/><br/>
<tt class="docutils literal">(setf min <span class="pre">'((a</span> b c) (b c) (c <span class="pre">d)))</span></tt><br/><br/>

(defun shortest-path (start end net)
  (bfs end (list (list start)) net))

(defun bfs (end queue net)
  (if (null queue)
      nil
      (let ((path (car queue)))
        (let ((node (car path)))
          (if (eql node end)
              (reverse path)
              (bfs end
                   (append (cdr queue)
                           (new-paths path node net))
                   net))))))

(defun new-paths (path node net)
  (mapcar #'(lambda (n)
              (cons n path))
          (cdr (assoc node net))))
</pre>
图 3.12 广度优先搜索(breadth-first search)<br/><br/>

<img alt="https://github.com/JuanitoFatas/acl-chinese/raw/master/images/Figure-3.13.png" src="https://github.com/JuanitoFatas/acl-chinese/raw/master/images/Figure-3.13.png" />

图 3.13 最小网络<br/><br/>
要找到从节点 <tt class="docutils literal">a</tt> 可以到达的节点，我们可以：<br/><br/>

&gt; (cdr (assoc 'a min))
(B C)
</pre>
图 3.12 程序的工作方式为使用广度优先的方式搜索网络。要使用广度优先搜索，你需要维护一个含有未探索节点的队列。每一次你到达一个节点，检查这个节点是否是你要的。如果不是，你把这个节点的子节点加入队列的尾端，并从队列起始选一个节点，从这继续搜索。藉由总是把较深的节点放在队列尾端，我们确保网络一次被搜索一层。<br/><br/>
图 3.12 中的代码较不复杂地表示这个概念。我们不仅想要找到节点，还想保有我们怎么到那的纪录。所以与其维护一个具有节点的队列 (queue)，我们维护一个已知路径的队列，每个已知路径都是一列节点。当我们从队列取出一个元素继续搜索时，它是一个含有队列前端节点的列表，而不只是一个节点而已。<br/><br/>
函数 <tt class="docutils literal">bfs</tt> 负责搜索。起初队列只有一个元素，一个表示从起点开始的路径。所以 <tt class="docutils literal"><span class="pre">shortest-path</span></tt> 呼叫 <tt class="docutils literal">bfs</tt> ，并传入 <tt class="docutils literal">(list (list start))</tt> 作为初始队列。<br/><br/>
<tt class="docutils literal">bfs</tt> 函数第一件要考虑的事是，是否还有节点需要探索。如果队列为空， <tt class="docutils literal">bfs</tt> 返回nil 指出没有找到路径。如果还有节点需要搜索， <tt class="docutils literal">bfs</tt> 检视队列前端的节点。如果节点的 <tt class="docutils literal">car</tt> 部分是我们要找的节点，我们返回这个找到的路径，并且为了可读性的原因我们反转它。如果我们没有找到我们要找的节点，它有可能在现在节点之后，所以我们把它的子节点（或是每一个子路径）加入队列尾端。然后我们递回地呼叫 <tt class="docutils literal">bfs</tt> 来继续搜寻剩下的队列。<br/><br/>
因为 <tt class="docutils literal">bfs</tt> 广度优先地搜索，第一个找到的路径会是最短的，或是最短之一：<br/><br/>

&gt; (shortest-path 'a 'd min)
(A C D)
</pre>
这是队列在我们连续呼叫 <tt class="docutils literal">bfs</tt> 看起来的样子：<br/><br/>

((A))
((B A) (C A))
((C A) (C B A))
((C B A) (D C A))
((D C A) (D C B A))
</pre>
在队列中的第二个元素变成下一个队列的第一个元素。队列的第一个元素变成下一个队列尾端元素的 <tt class="docutils literal">cdr</tt> 部分。<br/><br/>
在图 3.12 的代码不是搜索一个网络最快的方法，但它给出了列表具有多功能的概念。在这个简单的程序中，我们用三种不同的方式使用了列​​表：我们使用一个符号的列表来表示路径，一个路径的列表来表示在广度优先搜索中的队列 <a class="footnote-reference" href="#id5" id="id2">[4]</a> ，以及一个关联列表来表示网络本身。<br/><br/>


<h1>3.16 垃圾 (Garbages)</h1>
有很多原因可以使列表变慢。列表提供了循序存取而不是随机存取，所以列表取出一个指定的元素比阵列慢，同样的原因，录音带取出某些东西比在光盘上慢。电脑内部里， <em>Cons</em> 对象倾向于用指针表示，所以走访一个列表意味着走访一系列的指针，而不是简单地像阵列一样增加索引值。但这两个所花的代价与配置及回收 <em>Cons</em> 核 (cons cells)比起来小多了。<br/><br/>
<em>自动内存管理</em> ( <em>Automatic memory management</em> )是 Lisp 最有价值的特色之一。 Lisp 系统维护着一段內存称之为堆叠 ( <em>Heap</em> )。系统持续追踪堆叠当中没有使用的内存，把这些内存发放给新产生的对象。举例来说，函数 <tt class="docutils literal">cons</tt> ，返回一个新配置的Cons 对象。从堆叠中配置内存有时候通称为 <em>consing</em> 。<br/><br/>
如果内存永远没有释放， Lisp 会因为创建新对象把内存用完，而必须要关闭。所以系统必须周期性地通过搜索堆叠 (heap)，寻找不需要再使用的内存。不需要再使用的内存称之为垃圾 ( <em>garbage</em> )，而清除垃圾的动作称为垃圾回收 ( <em>garbage collection</em> 或 GC)。<br/><br/>
垃圾是从哪来的？让我们来创造一些垃圾：<br/><br/>

&gt; (setf lst (list 'a 'b 'c))
(A B C)
&gt; (setf lst nil)
NIL
</pre>
一开始我们呼叫 <tt class="docutils literal">list</tt> ， <tt class="docutils literal">list</tt> 呼叫 <tt class="docutils literal">cons</tt> ，在堆叠上配置了一个新的 <em>Cons</em> 对象。在这个情况我们创出三个 <em>Cons</em> 对象。之后当我们把 <tt class="docutils literal">lst</tt> 设为 <tt class="docutils literal">nil</tt> ，我们没有任何方法可以再存取 <tt class="docutils literal">lst</tt> ，列表 <tt class="docutils literal">(a b c)</tt> 。 <a class="footnote-reference" href="#id6" id="id3">[5]</a><br/><br/>
因为我们没有任何方法再存取列表，它也有可能是不存在的。我们不再有任何方式可以存取的对象叫做垃圾。系统可以安全地重新使用这三个 <em>Cons</em> 核。<br/><br/>
这种管理內存的方法，给程序員带来极大的便利性。你不用显式地配置 (allocate)或释放 (dellocate)內存。这也表示了你不需要处理因为这么做而可能产生的臭虫。內存泄漏 (Memory leaks)以及迷途指针 (dangling pointer)在 Lisp 中根本不可能发生。<br/><br/>
但是像任何的科技进步，如果你不小心的话，自动內存管理也有可能对你不利。使用及回收堆叠所带来的代价有时可以看做 <tt class="docutils literal">cons</tt> 的代价。这是有理的，除非一个程序从来不丢弃任何东西，不然所有的 <em>Cons</em> 对象终究要变成垃圾。 Consing 的问题是，配置空间与清除內存，与程序的常规运作比起来花费昂贵。近期的研究提出了大幅改善內存回收的演算法，但是 consing 总是需要代价的，在某些现有的 Lisp 系统中，代价是昂贵的。<br/><br/>
除非你很小心，不然很容易写出过度显式创建 cons 对象的程序。举例来说， <tt class="docutils literal">remove</tt> 需要复制所有的 <tt class="docutils literal">cons</tt> 核，直到最后一个元素从列表中移除。你可以藉由使用破坏性的函数避免某些 consing，它试​​着去重用列表的结构作为参数传给它们。破坏性函数会在 12.4 节讨论。<br/><br/>
当写出 <tt class="docutils literal">cons</tt> 很多的程序是如此简单时，我们还是可以写出不使用 <tt class="docutils literal">cons</tt> 的程序。典型的方法会是写出一个纯函数风格，使用很多列表的第一版程序。当程序进化时，你可以在代码的关键部份使用破坏性函数以及/或别种资料结构。但这很难给出通用的建议，因为有些 Lisp 实现，內存管理处理得相当好，以致于使用 <tt class="docutils literal">cons</tt> 有时比不使用 <tt class="docutils literal">cons</tt> 还快。这整个议题在 13.4 做更进一步的细部讨论。<br/><br/>
无论如何 Consing 在原型跟实验时是好的。而且如果你利用了列表给你带来的灵活性，你有较高的可能写出后期可存活下来的程序。<br/><br/>


<h1>Chapter 3 总结 (Summary)</h1>
<ol class="arabic simple">
<li>一个 <em>Cons</em> 是一个含两部分的数据结构。列表用链结在一起的 <em>Cons</em> 组成。</li>
<li>判断式 <tt class="docutils literal">equal</tt> 比 <tt class="docutils literal">eql</tt> 来得不严谨。基本上，如果传入参数印出来的值一样时，返回真。</li>
<li>所有 Lisp 对象表现得像指针。你永远不需要显式操作指针。</li>
<li>你可以使用 <tt class="docutils literal"><span class="pre">copy-list</span></tt> 复制列表，并使用 <tt class="docutils literal">append</tt> 来连接它们的元素。</li>
<li>游程编码是一个餐厅中使用的简单压缩演算法。</li>
<li>Common Lisp 有由 <tt class="docutils literal">car</tt> 与 <tt class="docutils literal">cdr</tt> 定义的多种存取函数。</li>
<li>映成函数应用函数至逐项的元素，或逐项的列表尾端。</li>
<li>嵌套列表的操作有时被考虑为树的操作。</li>
<li>要判断一个递归函数是否​​正确，你只需要考虑是否包含了所有情况。</li>
<li>列表可以用集合表示。数个内置函数把列表当作集合。</li>
<li>关键字参数是选择性的，并不是由位置所识别，是用符号前面的特殊标签来识别。</li>
<li>列表是序列的子类型。 Common Lisp 有大量的序列函数。</li>
<li>一个不是正规列表的 cons 称之为点状列表。</li>
<li>用 cons 对象作为元素的列表，可以拿来表示对应关系。这样的列表称为关联列表(assoc-lists)。</li>
<li>自动内存管理拯救你处理内存配置的烦恼，但制造过多的垃圾会使程序变慢。</li>
</ol>


<h1>Chapter 3 习题 (Exercises)</h1>
<ol class="arabic simple">
<li>用箱子表示法表示以下列表：</li>
</ol>

(a) (a b (c d))
(b) (a (b (c (d))))
(c) (((a b) c) d)
(d) (a (b . c) d)
</pre>
<ol class="arabic simple" start="2">
<li>写一个保留原本列表中元素顺序的 <tt class="docutils literal">union</tt> 版本：</li>
</ol>

&gt; (new-union '(a b c) '(b a d))
(A B C D)
</pre>
<ol class="arabic simple" start="3">
<li>定义一个函数，接受一个列表并返回一个列表，指出相等元素出现的次数，并由最常见至最少见的排序：</li>
</ol>

&gt; (occurrences '(a b a d a c d c a))
((A . 4) (C . 2) (D . 2) (B . 1))
</pre>
<ol class="arabic simple" start="4">
<li>为什么 <tt class="docutils literal">(member '(a) <span class="pre">'((a)</span> <span class="pre">(b)))</span></tt> 返回 nil？</li>
<li>假设函数 <tt class="docutils literal">pos+</tt> 接受一个列表并返回把每个元素加上自己的位置的列表：</li>
</ol>

&gt; (pos+ '(7 5 1 4))
(7 6 3 7)
</pre>
使用 (a) 递归 (b) 迭代 (c) <tt class="docutils literal">mapcar</tt> 来定义这个函数。<br/><br/>
<ol class="arabic simple" start="6">
<li>经过好几年的审议，政府委员会决定列表应该由 <tt class="docutils literal">cdr</tt> 指向第一个元素，而 <tt class="docutils literal">car</tt> 指向剩下的列表。定义符合政府版本的以下函数：</li>
</ol>

(a) cons
(b) list
(c) length (for lists)
(d) member (for lists; no keywords)
</pre>
<strong>勘误:</strong> 要解决 3.6 (b)，你需要使用到 6.3 节的参数 <tt class="docutils literal">&amp;rest</tt> 。<br/><br/>
<ol class="arabic simple" start="7">
<li>修改图 3.6 的程序，使它使用更少 cons 核。 （提示：使用点状列表）</li>
<li>定义一个函数，接受一个列表并用点状表示法印出：</li>
</ol>

&gt; (showdots '(a b c))
(A . (B . (C . NIL)))
NIL
</pre>
<ol class="arabic simple" start="9">
<li>写一个程序来找到 3.15 节里表示的网络中，最长有限的路径 (不重复)。网络可能包含循环。</li>
</ol>
脚注<br/><br/>
<table class="docutils footnote" frame="void" id="id4" rules="none">
<colgroup><col class="label" /><col /></colgroup>
<tbody valign="top">
<tr><td class="label"><a class="fn-backref" href="#id1">[3]</a></td><td>这个叙述有点误导，因为只要是对任何东西都不返回nil 的函数，都不是正规列表。如果给定一个循环 cdr 列表(cdr-circular list)，它会无法终止。循环列表在 12.7 节 讨论。</td></tr>
</tbody>
</table>
<table class="docutils footnote" frame="void" id="id5" rules="none">
<colgroup><col class="label" /><col /></colgroup>
<tbody valign="top">
<tr><td class="label"><a class="fn-backref" href="#id2">[4]</a></td><td>12.3 小节会展示更有效率的队列实现方式。</td></tr>
</tbody>
</table>
<table class="docutils footnote" frame="void" id="id6" rules="none">
<colgroup><col class="label" /><col /></colgroup>
<tbody valign="top">
<tr><td class="label"><a class="fn-backref" href="#id3">[5]</a></td><td>事实上，我们有一种方式来存取列表。全域变量 <tt class="docutils literal">*</tt> , <tt class="docutils literal">**</tt> , 以及 <tt class="docutils literal">***</tt> 总是设定为最后三个顶层所返回的值。这些变量在除错的时候很有用。</td></tr>
</tbody>
</table>


